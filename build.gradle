plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'com.code'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// jackson
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'

	// development tools
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// https://mvnrepository.com/artifact/io.rest-assured/rest-assured
	testImplementation 'io.rest-assured:rest-assured:5.3.0'
}

jacocoTestReport {
    reports {
        // XML形式のレポートを出力する
        xml.required = true
        // HTML形式のレポートを出力する
        html.required = true
        
        // XML形式のレポートの出力場所
        xml.outputLocation = file("${rootDir}/build/reports/coverage/${project.name}/jacocoTestReport.xml")
        // HTML形式のレポートの出力場所
        html.outputLocation = file("${rootDir}/build/reports/coverage/${project.name}/html")
    }

	// カバレッジ取得対象外とするパスを追加
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
				// 例："com/code/skelton/shared/*"
            ])
        }))
    }
}
tasks.named('check') {
    dependsOn tasks.named('jacocoTestReport', JacocoReport) 
}

test {
    filter {
        // explicitly exclude integration tests
        excludeTestsMatching "com.code.skelton.integration.*"
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

// テスト実行時、APIサーバーとDBサーバーを起動させる必要がある
tasks.register("apiTest", Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	useJUnitPlatform()
	filter {
		includeTestsMatching "com.code.skelton.integration.*"
	}
	// プロパティを取得し、デフォルト値(local)を設定
    def activeProfile = project.hasProperty('profile') ? project.profile : 'local'

    doFirst {
        println "Using profile: ${activeProfile}"
    }

    // テスト実行時にプロパティを設定
    systemProperty 'spring.profiles.active', activeProfile

}
